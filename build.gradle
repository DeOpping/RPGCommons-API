plugins {
    id 'java'
    id 'com.gradleup.shadow' version '8.3.3'
    id 'io.papermc.paperweight.userdev' version '2.0.0-beta.8'
    id 'maven-publish'
}

apply plugin: 'maven-publish'

group = 'dev.fabled'
version = "${versionMajor}.${versionMinor}.${versionPatch}"
if (hotfix == "true") {
    version += "-HOTFIX-${versionHotfix}"
}

repositories {
    mavenCentral()
    maven { url = 'https://repo.extendedclip.com/content/repositories/placeholderapi/' }
}

dependencies {
    testImplementation platform('org.junit:junit-bom:5.10.0')
    testImplementation 'org.junit.jupiter:junit-jupiter'
    paperweight.paperDevBundle(paperDevBundleVersion)
    compileOnly 'me.clip:placeholderapi:2.11.6'
}

test {
    useJUnitPlatform()
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

tasks.register('javadocJar', Jar) {
    dependsOn classes
    archiveClassifier = 'javadoc'
    from javadoc.destinationDir
}

publishing {
    publications {
        maven(MavenPublication) {
            from components.java
            artifact javadocJar
        }
    }
}